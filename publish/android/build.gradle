// Top-level build file where you can add configuration options common to all sub-projects/modules.

// Disable the stripping task itself because it can't distinguish between debug/release.
project.gradle.startParameter.excludedTaskNames.add("stripDebugDebugSymbols")

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

def getCommitTag() {
    return "git describe --tags --exact-match".execute().text.trim() ?: "untagged"
}

subprojects {

    ext.version_number       = project.hasProperty('ci') ? (getCommitTag() - ~/^v/) : (ringrtcVersion ?: "0.0.1-SNAPSHOT")
    ext.group_info           = "org.signal"

    ext.debug_jni_lib_dirs   = project.hasProperty("debugRingrtcLibDirs") ? Eval.me(debugRingrtcLibDirs) : []
    ext.debug_output_dir     = project.hasProperty("debugOutputDir") ? debugOutputDir : "/tmp/debug"
    ext.release_jni_lib_dirs = project.hasProperty("releaseRingrtcLibDirs") ? Eval.me(releaseRingrtcLibDirs) : []
    ext.release_output_dir   = project.hasProperty("releaseOutputDir") ? releaseOutputDir : "/tmp/release"

    ext.proguard_file = rootDir.getAbsolutePath() + '/proguard-rules.pro'

    if (JavaVersion.current().isJava8Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
