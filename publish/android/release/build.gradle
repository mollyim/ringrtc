apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

archivesBaseName = "ringrtc-android"
version          = version_number
group            = group_info

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    sourceSets {
        main {
            jniLibs.srcDirs = release_jni_lib_dirs
        }
    }

    defaultConfig {
        consumerProguardFiles proguard_file
    }

    variantFilter { variant ->
        if (variant.buildType.name.equals('debug')) {
            variant.setIgnore(true)
        }
    }
}

dependencies {
    for (String dir : release_jni_lib_dirs) {
        implementation fileTree(dir: dir, include: ['*.jar'])
    }
    testImplementation 'junit:junit:4.12'
}

publishing {
    repositories {
        mavenLocal()
    }

    publications {
        ringrtc(MavenPublication) {
            groupId = 'im.molly'
            artifactId = archivesBaseName

            artifact("$buildDir/outputs/aar/${archivesBaseName}-release.aar")

            pom {
                name = archivesBaseName
                description = 'RingRTC for Android'
                url = 'https://github.com/mollyim/ringrtc/'

                scm {
                    url = 'https://github.com/mollyim/ringrtc/'
                    connection = 'scm:git:git://github.com/mollyim/ringrtc.git'
                    developerConnection = 'scm:git:git@github.com:mollyim/ringrtc.git'
                }

                licenses {
                    license {
                        name = 'GPLv3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key  = System.getenv('BINTRAY_KEY')
    publications = ['ringrtc']
    publish = true
    override = true
    pkg {
        repo = 'maven'
        userOrg = 'mollyim'
        name = archivesBaseName
        vcsUrl = 'https://github.com/mollyim/ringrtc.git'
        licenses = ['GPL-3.0']
    }
}

tasks.withType(AbstractPublishToMaven) {
    dependsOn release:assemble
}

bintrayUpload {
    dependsOn release:assemble
}

task installArchives {
    dependsOn publishRingrtcPublicationToMavenLocal
}

task uploadArchives {
    dependsOn bintrayUpload
}
