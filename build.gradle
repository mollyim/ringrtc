// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
}

plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
}

def versionProperties = new Properties()
file("config/version.properties").withInputStream { versionProperties.load(it) }

if (!project.hasProperty("ringrtcVersion")) {
    ext.ringrtcVersion =
        "${versionProperties.getProperty("ringrtc.version.major")}." +
        "${versionProperties.getProperty("ringrtc.version.minor")}." +
        "${versionProperties.getProperty("ringrtc.version.revision")}"
}
ext.isReleaseVersion = ringrtcVersion.indexOf("-") == -1;

ext.webrtc_version = "${versionProperties.getProperty("webrtc.version")}"

group = "org.signal"
version = ringrtcVersion

task clean(type: Delete) {
    delete rootProject.buildDir
}

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint" << "-Xlint:-serial"
    }
}

nexusPublishing {
    repositories {
        sonatype {
            if (project.hasProperty("signalSonatypeUsername") && project.hasProperty("signalSonatypePassword")) {
                username = signalSonatypeUsername
                password = signalSonatypePassword
            }
            // This is the recommended configuration from the README for the plugin we use, gradle-nexus/publish-plugin.
            // The URLs are from https://central.sonatype.org/publish/publish-portal-ossrh-staging-api/#configuration
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}
